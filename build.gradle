buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'java'
apply plugin: 'maven'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
  maven {
    url "http://dvs1.progwml6.com/files/maven"
  }
}

sourceSets.main{
    java{
        srcDirs = ['src/main/java']
    }
    resources{
        srcDirs = ['src/main/resources']
    }
}

ext.configFile = file "build.properties"

configFile.withReader {
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

version = config.mod_version
group= "me.superckl.recipetooltips"
archivesBaseName = "RecipeTooltips"

minecraft {
    useDepAts = true

    version = config.minecraft_version + "-" + config.forge_version
    mappings = "snapshot_20160109"

    replaceIn "reference/ModData.java"
    replace "@VERSION@", "${config.mod_version}.${System.env.BUILD_NUMBER ?: 0}"

    runDir = "eclipse"
}

version = "${config.minecraft_version}-${config.mod_version}.${System.getenv("BUILD_NUMBER") ?: 0}"

dependencies {
    compile "org.projectlombok:lombok:1.16.6"
	deobfCompile "mezz.jei:jei_1.8.9:2.21.0.98"
}

processResources
{
    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand([
        'mod_version': project.config.mod_version,
        'minecraft_version': project.config.minecraft_version,
        ])
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }

	rename '(.+_at.cfg)', 'META-INF/$1'
}

task devJar(type: Jar, dependsOn: 'classes') {
    from(sourceSets.main.output) {
        include '**'
    }

    extension = 'jar'
    classifier = 'dev'
}

artifacts {
  archives devJar
}

jar {
    manifest {
        attributes 'ModSide': 'CLIENT'
		attributes 'FMLAT': 'recipetooltips_at.cfg'
    }
}
